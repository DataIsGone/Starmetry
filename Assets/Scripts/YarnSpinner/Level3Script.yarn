title: Intro3
tags:
---
<<set $firstConvo to false>>
<<set $irrigationComplete to false>>
<<set $wheelComplete to false>>

// GOALS
<<set $talkedToUnicorn to false>>
<<set $allQuestionsAsked to false>>
<<set $wheelPlaced to false>>

Birbby: Hey, I'm starting from here.

===

title: BusinessIntro
---
<<if $firstConvo is false>>
    Business: Hello.
    Business: Rect Vol is {get_piece("answer")}.
    Business: Cylinder Vol is {get_piece2("answer")}.
    <<set $firstConvo to true>>
    <<set $talkedToUnicorn to true>>
<<else>>
    <<if $wheelComplete is true and $irrigationComplete is true>>
        <<jump CompletedLevel3>>
    <<else>>
        Business: You're back. What do you need to know?
        // IRRIGATION PROBLEM (CURR PROBLEM 1)
        -> Irrigation System
            Business: What did you want to know about the irrigation system?
            -> Give Volume
                Business: You know how much water it can hold?
                <<math_answering>>
                <<jump AnsweringIrrigation>>
        // WATER WHEEL PROBLEM (CURR PROBLEM 2)
        -> Water Wheel
            Business: What did you want to ask about the water wheel?
            -> Give Wheel <<if check_has_wheel() is true>>
                <<jump AnsweringWaterWheel>>
            -> Question 1
                Business: Answer to Question 1
            -> Question 2
                Business: Answer to Question 2
            -> Question 3
                Business: Answer to Question 3
        -> Nevermind
            Business: Get back to it.
    <<endif>>
<<endif>>
===

title: AnsweringWaterWheel
---
Business: You found a wheel that works for me?
<<if compare(2) is true>>
    Business: That's the correct one!
    <<set $wheelComplete to true>>
    <<if $wheelComplete is true and $irrigationComplete is true>>
        <<jump CompletedLevel3>>
    <<else>>
        Business: What about the irrigation box?
    <<endif>>
<<else>>
    Business: I don't think that's the right wheel.
    Business: Why don't you put that one back where you found it?
    Business: And come back to me with a different one!
<<endif>>
===

title: AnsweringIrrigation
---
<<if compare(1) is true>>
    Business: Oh, that's how much water it can hold...
    <<set $irrigationComplete to true>>
    <<if $wheelComplete is true and $irrigationComplete is true>>
        <<jump CompletedLevel3>>
    <<else>>
        Business: Can you help me with the wheel now?
    <<endif>>
<<else>>
    Business: That doesn't sound correct to me.
<<endif>>
===

title: CompletedLevel3
---
Business: Oh WOW. You actually did it.
Business: I'll start up the irrigation machine.
Business: Well, actually...
Business: Could you put the wheel on that machine there for me?

// once player puts wheel on machine
Business: Alright, starting!
<<l3_complete>>

Birbby: After that star!

// player and Birbby move off screen
===

title: L3Goals
---
<<if $talkedToUnicorn is false>>
    Birbby: We should be looking around for someone to talk to.
    Birbby: Chances are they know how to work that box thing.
<<elseif $irrigationComplete is true and $wheelComplete is true>>
    Birbby: We need to place that wheel on the axel.
    Birbby: The axel is atttached to the machine.
<<elseif $allQuestionsAsked is true>>
    Birbby: You got all of the information you needed, right?
    Birbby: We can take that information and solve. These are volume problems.
    Birbby: You can talk to me if you need a reminder on how to solve those.
    Birbby: And if you need the pieces to either problem, I'm sure you can talk to that guy again.
    Bribby: As weird as he is.
<<elseif $allQuestionsAsked is false>>
    <<if $irrigationComplete is false and $wheelComplete is true>>
        Birbby: So the last thing we need to do is figure out how much water that water box can hold.
        Birbby: He could tell us what he knows about the machine he wants to use so badly.
        Birbby: You know, things like how wide the box is, how long it is...
        Birbby: Let's just talk to him.
    <<elseif $irrigationComplete is true and $wheelComplete is false>>
        Birbby: The last thing we need to do is help him find this water wheel because he's too lazy to do it himself.
        Birbby: Or, you know, because he's the "idea guy". That's his "job".
        Birbby: Anyways, it's best to talk to him to see if he knows anything about the wheel he needs.
        Birbby: And once you know the volume of the wheel, we need to run around and find it.
        Birbby: Might as well talk to him again. Or look for that wheel.
    <<else>>
        Birbby: I know he's weird, but he knows best about what he needs. At least I hope he does.
        Birbby: Go and talk to him about the water wheel and box.
        Birbby: You can ask him some questions to help us help him. And my stars, does this guy need help.
    <<endif>>
<<endif>>



===